apply plugin: 'com.android.library'

android {
    compileSdkVersion 31
    defaultConfig {
        versionCode Integer.parseInt(project.VERSION_CODE)
        minSdkVersion 14
        targetSdkVersion 31
        versionName project.VERSION_NAME
    }
    signingConfigs {
        config {
            keyAlias 'BuildmLearn'
            keyPassword 'buildmlearn'
            storeFile file('debugKey.jks')
            storePassword 'buildmlearn'
        }
    }
    def URL_SCHEME_HTTPS = '"https"'
    // use this flag to enable use of static data on debug builds
    // will cause no side effect even if it is left in release builds
    def SHOULD_USE_MOCK_DATA_IN_DEBUG = 'false'
    def DACDN_URL = '"dacdn.visualwebsiteoptimizer.com"'
    def CHINA_DACDN_URL = '"cdn-cn.vwo-analytics.com"'
    def SOCKET_URL = '"https://mobilepreview.vwo.com:443/"'
    buildTypes {
        release {
            signingConfig signingConfigs.config
            it.buildConfigField 'String', 'SCHEME', URL_SCHEME_HTTPS
            it.buildConfigField 'Boolean', 'ENABLE_SOCKET_LOGS', "false"
            it.buildConfigField 'int', 'VERSION_CODE', "${project.VERSION_CODE}"
            it.buildConfigField 'String', 'VERSION_NAME', "\"${project.VERSION_NAME}\""

            postprocessing {
                removeUnusedCode true
                removeUnusedResources false
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
        debug {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            it.buildConfigField 'String', 'SCHEME', URL_SCHEME_HTTPS
            it.buildConfigField 'Boolean', 'ENABLE_SOCKET_LOGS', "false"
            it.buildConfigField 'int', 'VERSION_CODE', "${project.VERSION_CODE}"
            it.buildConfigField 'String', 'VERSION_NAME', "\"${project.VERSION_NAME}\""
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    buildTypes.each {
        it.buildConfigField 'Boolean', 'SHOULD_USE_MOCK_DATA_IN_DEBUG', SHOULD_USE_MOCK_DATA_IN_DEBUG
        it.buildConfigField 'String', 'DACDN_URL', DACDN_URL
        it.buildConfigField 'String', 'SOCKET_URL', SOCKET_URL
        it.buildConfigField 'String', 'CHINA_DACDN_URL', CHINA_DACDN_URL
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "failed", "skipped", "passed"
                    showStackTraces = true
                    exceptionFormat = "full"
                }
                maxParallelForks = 8
                forkEvery = 20
            }
        }
    }
}

dependencies {
    // Required -- Android JSON Test Dependency
    testImplementation 'org.json:json:20210307'
    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.1'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-module-junit4-rule:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-classloading-xstream:2.0.0-beta.5'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.7'
    implementation('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
}

apply from: '../gradle-mvn-push.gradle'