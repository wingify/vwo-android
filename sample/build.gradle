apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        versionCode 9
        minSdkVersion 14
        targetSdkVersion 27
        applicationId "com.vwo.sample"
        versionCode 10
        versionName "10.0.0"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    // Specifies a flavor dimension.
    flavorDimensions "vwo"

    def DEBUG_LOCAL_VWO_API_KEY=project.properties['DEBUG_LOCAL_VWO_API_KEY'] ?: '""'
    def DEBUG_REMOTE_VWO_API_KEY=project.properties['DEBUG_REMOTE_VWO_API_KEY'] ?: '""'
    def RELEASE_VWO_API_KEY=project.properties['RELEASE_VWO_API_KEY'] ?: '""'
    def TESTING_VWO_API_KEY=project.properties['TESTING_VWO_API_KEY'] ?: '""'

    productFlavors {
        local {
            it.buildConfigField 'String', 'VWO_API_KEY', DEBUG_LOCAL_VWO_API_KEY
            dimension "vwo"
        }
        remote {
            it.buildConfigField 'String', 'VWO_API_KEY', DEBUG_REMOTE_VWO_API_KEY
            dimension "vwo"
        }
        production {
            it.buildConfigField 'String', 'VWO_API_KEY', RELEASE_VWO_API_KEY
            dimension "vwo"
        }
        manualTesting {
            it.buildConfigField 'String', 'VWO_API_KEY', TESTING_VWO_API_KEY
            dimension "vwo"
        }
    }

    variantFilter { variant ->
        if (variant.name == "localRelease" || variant.name == "remoteRelease" || variant.name == "productionDebug" || variant.name == "manualTestingRelease") {
            // Gradle ignores any variants that satisfy the conditions above.
            variant.setIgnore(true)
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
//    implementation project(':library')
    implementation 'com.vwo:mobile:2.1.0@aar'
    debugImplementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    releaseImplementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.airbnb:deeplinkdispatch:3.1.1'
    annotationProcessor 'com.airbnb:deeplinkdispatch-processor:3.1.0'
}